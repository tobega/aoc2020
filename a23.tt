def input: [7,9,2,8,4,5,1,3,6];

processor Node
  @: {value: ($)"1"};
  source value
    $@Node.value!
  end value
  source next
    $@Node.next !
  end next
  sink setNext
    ..|@Node: (next: $);
  end setNext
  templates replaceNext
    $@Node.next !
    @Node.next: $;
  end replaceNext
end Node

templates moveFast&{times:}
  @: {move: 1"1", nodes: [1..$::length -> Node]};
  def input: $;
  2..$@.nodes::length -> \(
    def i: $;
    $@moveFast.nodes($input($i)) -> !@moveFast.nodes($input($i-1))::setNext
  \) -> !VOID
  $@.nodes($input(1)) -> !@.nodes($input(last))::setNext

  $@.nodes($input(1)) -> #
  
  when <?($@.move <..$times>)> do
    @.move: $@.move + 1"1";
    def this: $;
    def one: $this::next;
    def two: $one::next;
    def three: $two::next;
    def destination: $this::value - 1"1" -> \(
      when <..0"1"> do ($ + $@moveFast.nodes::length)"1" -> #
      when <=$one::value|=$two::value|=$three::value> do $ - 1"1" -> #
      otherwise $!
    \);
    $three::next -> !this::setNext
    $one -> @.nodes($destination::raw)::replaceNext -> !three::setNext
    $this::next -> #

  when <?($::value <=1"1">)> do $!
  otherwise $::next -> #
end moveFast

templates finalizeFast
  $::next -> #
  when <?($::value <~=1"1">)> do $::value -> $::raw ! $::next -> #
end finalizeFast

// part 1
$input -> moveFast&{times: 100"1"} -> '$ -> finalizeFast;
' -> !OUT::write

test 'move fast'
  assert [[3,8,9,1,2,5,4,6,7] -> moveFast&{times: 10} -> finalizeFast] <=[9, 2, 6, 5, 8, 3, 7, 4]> 'fast example 10'
end 'move fast'

// part 2
[$input..., $input::length+1..1000000] -> moveFast&{times: 10000000"1"}
  -> ($::next -> $::value) * ($::next -> $::next -> $::value) -> '$;
' -> !OUT::write
