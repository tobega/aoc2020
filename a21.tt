use 'hash' stand-alone
use 'list' stand-alone

composer parseLabel
  {ingredients: [<ingredient>+], allergens: [<allergenList>?]}
  rule ingredient: <'\w+'> (<WS>?)
  rule allergenList: (<='(contains '>) <allergen>+ (<=')'>)
  rule allergen: <'\w+'> (<',? ?'>)
end parseLabel

test 'labels'
  assert 'mxmxvkd kfcds sqjhc nhms (contains dairy, fish)' -> parseLabel
    <={allergens:['dairy', 'fish'], ingredients:['mxmxvkd', 'kfcds', 'sqjhc', 'nhms']}> ''
end 'labels'

def foodLabels: [$IN::lines -> parseLabel];

def allergens: [] -> hash/Map&{key: :(allergen:), value: :(ingredients:)};

sink collateAllergens
  def food: $;
  $.allergens... -> {allergen: $, ingredients: $food.ingredients} -> allergens::put // emits previous, if any
    -> {allergen: $.allergen, ingredients: [$.ingredients... -> \(<?($food.ingredients <[<=$>]>)> $!\)]}
    -> allergens::put -> !VOID
end collateAllergens

$foodLabels... -> !collateAllergens

def safeIngredients: [$foodLabels... -> $.ingredients...] -> hash/Set;
$allergens::entries -> $.ingredients... -> safeIngredients::remove -> !VOID

// part 1
$foodLabels -> \(
  @: 0;
  $... -> $.ingredients... -> safeIngredients::isMember -> @: $@ + $;
  $@!
\) -> '$;
' -> !OUT::write

def identifiedAllergens: [] -> hash/Map&{key: :(ingredient:), value: :(allergen:)};

sink resolveAllergens
  @:[];
  $ -> #
  when <[](1..)> do
    $... -> \(
      when <{ingredients: <[](1)>}> do
        {ingredient: $.ingredients(1), allergen: $.allergen} -> identifiedAllergens::put -> !VOID
      when <{ingredients: <[](2..)>}> do 
        ..|@resolveAllergens: {allergen: $.allergen, ingredients: [$.ingredients... -> \(<?([$ -> identifiedAllergens::get] <=[]>)> $!\)]};
    \) -> !VOID
    def next: $@;
    @:[];
    $next -> #
end resolveAllergens

[$allergens::entries] -> !resolveAllergens

operator (a allergenAlphabetically b)
  (($a -> identifiedAllergens::get) list/naturalOrder ($b -> identifiedAllergens::get)) !
end allergenAlphabetically

// part 2
[$identifiedAllergens::entries -> $.ingredient] -> list/sort&{by: allergenAlphabetically}
  -> '$(1);$(2..last)... -> ',$;';
' -> !OUT::write
