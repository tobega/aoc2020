data count <"1">

composer bagRule
  { colour: <colour>, (<=' bags contain '>) contains: [<content|none>+]} (<='.'>)
  rule colour: <'\w+ \w+'>
  rule content: {count: <INT>, (<=' '>) colour: <colour>} (<' bags?(, )?'>)
  rule none: (<='no other bags'>)
end bagRule

def rules: [$IN::lines -> bagRule];

templates containerSearch
  @: {result: []};
  $ -> #
  $@.result!
  <> def required: $;
    $rules... -> \(
      when <{contains: <[<{colour: <=$required>}>]>}> do
        $.colour!
    \) -> \(
      when <~?($@containerSearch.result <[<=$>]>)> do
        ..|@containerSearch.result: $;
        $!
    \) -> #
end containerSearch

//part1
'shiny gold' -> containerSearch -> '$::length;
' -> !OUT::write

templates countBags
  @: 0;
  {count: 1, colour: $} -> #
  $@ !
  <> def current: $;
    @: $@ + $.count;
    $rules... -> \(when <{colour: <=$current.colour>}> do $.contains...!\)
      -> {count: $.count * $current.count, colour: $.colour} -> #
end countBags

//part2
'shiny gold' -> countBags -> $-1 ->'$;
' -> !OUT::write

test 'part2'
use shadowed core-system/
  processor StdIn
    source lines
      ['shiny gold bags contain 2 dark red bags.',
      'dark red bags contain 2 dark orange bags.',
      'dark orange bags contain 2 dark yellow bags.',
      'dark yellow bags contain 2 dark green bags.',
      'dark green bags contain 2 dark blue bags.',
      'dark blue bags contain 2 dark violet bags.',
      'dark violet bags contain no other bags.'
      ]...!
    end lines
  end StdIn

  def IN: $StdIn;
end core-system/

  assert 'shiny gold' -> countBags <=127> ''
end 'part2'
